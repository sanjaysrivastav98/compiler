<mainFunction> ===> MAIN SQO SQC <stmtOrFunctionDef> <stmtsAndFunctionDefs> END
<stmtsAndFunctionDefs> ===> <stmtOrFunctionDef> <stmtsAndFunctionDefs>|eps
<stmtOrFunctionDef> ===> <stmt>|<functionDef>
<stmt> ===> <declarationStmt>|<assignmentStmt>|<conditionalStmt>|<ioStmt>|<leftHandSide> ASSIGNOP  <funCallORsizeCallStmt>|<funCallStmt>
<funCallORsizeCallStmt> ===> <sizeCallStmt>|<funCallStmt>
<functionDef> ===> FUNCTION SQO <parameter_list> SQC ASSIGNOP FUNID SQO <parameter_list> SQC <stmtOrFunctionDef> <stmtsAndFunctionDefs> END SEMICOLON
<parameter_list> ===> <type> ID <remainingList>
<type> ===> INT|REAL|STRING|MATRIX
<remainingList> ===> COMMA <parameter_list>|eps
<declarationStmt> ===> <type> <var_list> SEMICOLON
<var_list> ===> ID <more_ids>
<more_ids> ===> COMMA <var_list>|eps
<assignmentStmt> ===> ID ASSIGNOP <rightHandSide> SEMICOLON
<rightHandSide> ===> <arithmeticExpression>|<sizeExpression>|<functionCall>
<leftHandSide> ===> SQO <var_list> SQC
<sizeCallStmt> ===> <sizeExpression> SEMICOLON
<conditionalStmt> ===> IF OP <booleanExpression> CL <stmt> <otherStmts> <elseORend>
<elseORend> ===> ELSE <otherStmts> ENDIF|ENDIF
<otherStmts> ===> <stmt> <otherStmts>|eps
<ioStmt> ===> READ OP ID CL SEMICOLON|PRINT OP ID CL SEMICOLON
<funCallStmt> ===> <functionCall> SEMICOLON
<functionCall> ===> FUNID OP <var_list1> CL 
<var_list1> ===> <var> <list_var>
<list_var> ===> COMMA <var_list1>|eps
<sizeExpression> ===> SIZE ID
<arithmeticExpression> ===> <term> <arithmeticExpression1>
<arithmeticExpression1> ===> <termOp> <arithmeticExpression>|eps
<termOp> ===> PLUS|MINUS
<term> ===> <factor> <term1>
<term1> ===> <factorOp> <term>|eps
<factorOp> ===> DIV|MUL
<factor> ===> OP <arithmeticExpression> CL|<var>
<booleanExpression> ===> OP <booleanExpression> CL <logicalOp> OP <booleanExpression> CL|NOT OP <booleanExpression> CL|<constrainedVars> <relationalOp> <constrainedVars>
<constrainedVars> ===> ID|NUM|RNUM
<var> ===> NUM|RNUM|STR|<IDORmatrixElement>
<IDORmatrixElement> ===> SQO <matrix1>|ID <matrix2>
<matrix1> ===> <row> SQC
<row> ===> <firstRow> <otherRows>
<firstRow> ===> NUM <otherEles>
<otherRows> ===> SEMICOLON <row>|eps
<otherEles> ===> COMMA <firstRow>|eps
<matrix2> ===> SQO NUM COMMA NUM SQC|eps
<logicalOp> ===> AND|OR 
<relationalOp> ===> LT|LE|EQ|GT|GE|NE
